(load "https://github.com/TimDeve/subprocess@v0.0.2")
(load "../sheriff.carp")

(use-all Subprocess SubprocessRes SubprocessOptions Maybe Array Result)

(deftype Post
  [id    Int
   title String
   body  String])

(defn main []
  (let [res (cmd (sps) &[@"curl" @"-sS" @"https://jsonplaceholder.typicode.com/posts"])
        code (exit-code &res)]
    (if (not (= @code 0))
      @code
      (match-ref (out &res)
        Nothing 1
        (Just std-out)
        (match (SheriffJSON.unmarshall (Array Post) std-out)
          (Result.Error err) (do (IO.println &err) 1)
          (Result.Success posts)
          (let-do [hundredth-post (unsafe-nth &posts 99)]
            (println* "id: "    (Post.id hundredth-post))
            (println* "title: " (Post.title hundredth-post))
            (println* "body: "  (Post.body hundredth-post))
            0))))))
